/*
 * Copyright 2017 Nicola Atzei
 */

import it.unica.tcs.bitcoinTM.AbsoluteTime
import it.unica.tcs.bitcoinTM.AddressLiteral
import it.unica.tcs.bitcoinTM.AddressType
import it.unica.tcs.bitcoinTM.AfterTimeLock
import it.unica.tcs.bitcoinTM.AndExpression
import it.unica.tcs.bitcoinTM.ArithmeticSigned
import it.unica.tcs.bitcoinTM.Between
import it.unica.tcs.bitcoinTM.BitcoinTMFactory
import it.unica.tcs.bitcoinTM.BooleanLiteral
import it.unica.tcs.bitcoinTM.BooleanNegation
import it.unica.tcs.bitcoinTM.BooleanType
import it.unica.tcs.bitcoinTM.Comparison
import it.unica.tcs.bitcoinTM.Compile
import it.unica.tcs.bitcoinTM.Constant
import it.unica.tcs.bitcoinTM.DateLiteral
import it.unica.tcs.bitcoinTM.Div
import it.unica.tcs.bitcoinTM.Equals
import it.unica.tcs.bitcoinTM.Hash160
import it.unica.tcs.bitcoinTM.Hash256
import it.unica.tcs.bitcoinTM.HashLiteral
import it.unica.tcs.bitcoinTM.HashType
import it.unica.tcs.bitcoinTM.HashableType
import it.unica.tcs.bitcoinTM.IfThenElse
import it.unica.tcs.bitcoinTM.Input
import it.unica.tcs.bitcoinTM.IntType
import it.unica.tcs.bitcoinTM.KeyLiteral
import it.unica.tcs.bitcoinTM.KeyType
import it.unica.tcs.bitcoinTM.Max
import it.unica.tcs.bitcoinTM.Min
import it.unica.tcs.bitcoinTM.Minus
import it.unica.tcs.bitcoinTM.NumberLiteral
import it.unica.tcs.bitcoinTM.OrExpression
import it.unica.tcs.bitcoinTM.Output
import it.unica.tcs.bitcoinTM.Parameter
import it.unica.tcs.bitcoinTM.PermittedType
import it.unica.tcs.bitcoinTM.Plus
import it.unica.tcs.bitcoinTM.PubKeyLiteral
import it.unica.tcs.bitcoinTM.PubkeyType
import it.unica.tcs.bitcoinTM.Reference
import it.unica.tcs.bitcoinTM.RelativeTime
import it.unica.tcs.bitcoinTM.Ripemd160
import it.unica.tcs.bitcoinTM.Script
import it.unica.tcs.bitcoinTM.Sha1
import it.unica.tcs.bitcoinTM.Sha256
import it.unica.tcs.bitcoinTM.Signature
import it.unica.tcs.bitcoinTM.SignatureLiteral
import it.unica.tcs.bitcoinTM.SignaturePlaceholder
import it.unica.tcs.bitcoinTM.SignatureType
import it.unica.tcs.bitcoinTM.Size
import it.unica.tcs.bitcoinTM.StringLiteral
import it.unica.tcs.bitcoinTM.StringType
import it.unica.tcs.bitcoinTM.Times
import it.unica.tcs.bitcoinTM.Transaction
import it.unica.tcs.bitcoinTM.TransactionHexLiteral
import it.unica.tcs.bitcoinTM.TransactionIDLiteral
import it.unica.tcs.bitcoinTM.TransactionType
import it.unica.tcs.bitcoinTM.Type
import it.unica.tcs.bitcoinTM.TypeVariable
import it.unica.tcs.bitcoinTM.Typeable
import it.unica.tcs.bitcoinTM.Versig
import it.unica.tcs.utils.ASTUtils
import it.unica.tcs.validation.AbstractBitcoinTMValidator
import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.util.EcoreUtil
import org.eclipse.xtext.EcoreUtil2
import it.unica.tcs.bitcoinTM.InputValue

system it.unica.tcs.xsemantics.BitcoinTMTypeSystem

validatorExtends AbstractBitcoinTMValidator

inject LambdaUtils lambdaUtils
inject extension ASTUtils astUtils

auxiliary {
    typesubstitution(TypeSubstitutions substitutions, Type original) : Type
    unify(EObject obj, TypeSubstitutions substitutions, Type left, Type right) : Type
    failIfTypeVariable(Type type, EObject obj)
    failIfNotPermitted(Type type, EObject obj)
}

judgments {
    typeExpression |- TypeSubstitutions substitutions |> Typeable exp : output Type
        error "Cannot type " + stringRep(exp)
        source exp
}

/*
 * auxiliary functions
 */

auxiliary failIfTypeVariable(Type type, EObject obj) {
    if (type instanceof TypeVariable) {
        fail
        error "Cannot infer the type"
        source obj
    }
}

auxiliary failIfNotPermitted(Type type, EObject obj) {
    if (!(type instanceof PermittedType)) {
        fail
        error "Basic type expected: int, string, bool, hash160, hash256, ripemd160, sha256, transaction, signature, key, address, pubkey"
        source obj
    }
}

auxiliary typesubstitution(TypeSubstitutions substitutions, Type type) {
    type
}

auxiliary typesubstitution(TypeSubstitutions substitutions, TypeVariable variable)
{
    var mapped = substitutions.mapped(variable.value)
    if (mapped !== null) {
        val result = EcoreUtil::copy(mapped)
        typesubstitution(substitutions, result) // recursive
    } else
        variable
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, Type actual, Type expected) {
    // if we get here we cannot unify the two types
    fail
    error 'Invalid type '+stringRep(actual)+", expected type "+stringRep(expected)
    source obj
    null
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, IntType t1, IntType t2) {
    EcoreUtil::copy(t1)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, HashType t1, HashType t2) {
    EcoreUtil::copy(t1)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, StringType t1, StringType t2) {
    EcoreUtil::copy(t1)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, BooleanType t1, BooleanType t2) {
    EcoreUtil::copy(t1)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, KeyType t1, KeyType t2) {
    EcoreUtil::copy(t1)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, AddressType t1, AddressType t2) {
    EcoreUtil::copy(t1)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, SignatureType t1, SignatureType t2) {
    EcoreUtil::copy(t1)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, TransactionType t1, TransactionType t2) {
    EcoreUtil::copy(t1)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, PubkeyType t1, PubkeyType t2) {
    EcoreUtil::copy(t1)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, TypeVariable typeVar, PermittedType basicType) {
    substitutions.add(typeVar.value, basicType)
    EcoreUtil.copy(basicType)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, PermittedType basicType, TypeVariable typeVar) {
    unify(obj, substitutions, typeVar, basicType)
}

auxiliary unify(EObject obj, TypeSubstitutions substitutions, TypeVariable left, TypeVariable right) {
    // unify both variables with a fresh new variable
    val fresh = lambdaUtils.createFreshTypeVariable
    substitutions.add(left.value, fresh)
    substitutions.add(right.value, fresh)
    fresh
}

// key is subtype of address: you can derive an address from its private part
auxiliary unify(EObject obj, TypeSubstitutions substitutions, KeyType t1, AddressType t2) {
    EcoreUtil::copy(t2)
}

// key is subtype of a pubkey: you can derive the public part from its private part
auxiliary unify(EObject obj, TypeSubstitutions substitutions, KeyType t1, PubkeyType t2) {
    EcoreUtil::copy(t2)
}

// pubkey is subtype of an address: you can derive the address from its pubkey
auxiliary unify(EObject obj, TypeSubstitutions substitutions, PubkeyType t1, AddressType t2) {
    EcoreUtil::copy(t2)
}



/*
 * Type system rules
 */
axiom SignaturePlaceholder
    G |- TypeSubstitutions substitutions |> SignaturePlaceholder p : BitcoinTMFactory::eINSTANCE.createSignatureType 

rule Parameter
    G |- TypeSubstitutions substitutions |> Parameter p : Type type
from {
    {
        p.type !== null
        type = EcoreUtil.copy(p.type)
    }
    or {
        type = lambdaUtils.createFreshTypeVariable
    }
    G.add(p, type)
}

rule Constant
    G |- TypeSubstitutions substitutions |> Constant c : Type type
from {
    {
        c.type !== null
        type = EcoreUtil.copy(c.type)
    }
    or {
        type = lambdaUtils.createFreshTypeVariable
    }

    substitutions.addVisited(c)

    G |- substitutions |> c.exp : var Type expType
    type = unify(c.exp, substitutions, expType, type)

    substitutions.removeVisited(c)

    failIfTypeVariable(type, c)
    c.type = type as PermittedType  // store the type in the AST
}

rule Reference
    G |- TypeSubstitutions substitutions |> Reference varRef : Type type
from {
    if (substitutions.isAlreadyVisited(varRef.ref)) {
        fail
            error "Found cyclic dependency"
            source varRef
    }

    if (varRef.ref instanceof Constant) {
        val const = varRef.ref as Constant
        G |- substitutions |> const : type
        
        if (!varRef.actualParams.^empty) {
            fail
                error "Constant reference cannot have actual parameters"
                source varRef
        }
    }
    else if (varRef.ref instanceof Parameter) {
        val param = varRef.ref as Parameter
        G |- substitutions |> param : type
        
        if (!varRef.actualParams.^empty) {
            fail
                error "Parameter reference cannot have actual parameters"
                source varRef
        }
    }
    else if (varRef.ref instanceof Transaction) {
        val tx = varRef.ref as Transaction

        G |- substitutions |> tx : type
        type = typesubstitution(substitutions, type)
        unify(tx, substitutions, type, BitcoinTMFactory::eINSTANCE.createTransactionType)

        if (varRef.actualParams.size != tx.params.size) {
            fail
                error "Invalid number of actual parameters."
                source varRef
        }

        // check the actual parameters type match the formal ones
        for (var i=0; i<varRef.actualParams.size; i++) {
            var actualParam = varRef.actualParams.get(i)
            var formalParam = tx.params.get(i)

            // get formal parameter type
            var formalParamType = env(G, formalParam, Type)
            // apply eventual substitutions to get the basic type
            formalParamType = typesubstitution(substitutions, formalParamType)
            // ensure it's basic
            failIfTypeVariable(formalParamType, formalParam)

            // all the actual parameters are well typed
            G |- substitutions |> actualParam : var Type actualParamType
            // apply eventual substitutions
            actualParamType = typesubstitution(substitutions, actualParamType)

            // the actual param type must be unifiable with the formal param type
            unify(actualParam, substitutions, actualParamType, formalParamType)

            // store the type in the AST
            formalParam.type = formalParamType as PermittedType
        }

    }
}

/*
 * - all the inputs are well typed
 * - all the outputs are well typed
 */
rule Transaction
    G |- TypeSubstitutions substitutions |> Transaction body : BitcoinTMFactory::eINSTANCE.createTransactionType
from {
    substitutions.addVisited(body)

    for (p : body.params) {
        G |- substitutions |> p : var Type t    // type the parameters, possibly TypeVariable
    }

    for (in : body.inputs) {
        G |- substitutions |> in : var Type t1  // all inputs are well typed
    }

    for (out : body.outputs) {
        G |- substitutions |> out : var Type t2 // all outputs are well typed
    }

    substitutions.removeVisited(body)

    // after typing the parameters, all should have a basic type (generic types are not allowed)
    for (p : body.params) {
        var ptype = env(G, p, Type)
        ptype = typesubstitution(substitutions, ptype)
        failIfTypeVariable(ptype, p)
        p.type = ptype as PermittedType
    }
}


/**
 * An input is well typed if:
 * - the body of the redeemed transaction is well typed
 * - all the expressions are well typed and the type is basic (not generic)
 * - if the redeemed tx has free variables, check that the actual types corresponds to the formal ones
 * - if the redeemed tx is user defined, all the expression type corresponds to the expected type of the parameter
 */
rule Input
    G |- TypeSubstitutions substitutions |> Input input : BitcoinTMFactory::eINSTANCE.createVoidType
from {

    if (!input.isPlaceholder) {

        val txType = BitcoinTMFactory::eINSTANCE.createTransactionType

        // the body of the redeemed transaction is well typed
        G |- substitutions |> input.txRef : var Type t1     // eventual tx parameters have type BasicType
        t1 = typesubstitution(substitutions, t1)
        unify(input.txRef, substitutions, t1, txType)

        val inputTxRef = input.txRef

        if (inputTxRef instanceof Reference
            && (inputTxRef as Reference).ref instanceof Transaction
        ) {
            val tx = (inputTxRef as Reference).ref as Transaction

            // check the actual parameters type match the formal ones (Script)
            var outputIndex = input.outpoint
            var outputScript = tx.outputs.get(new Long(outputIndex).intValue).script

            if (input.exps.size != outputScript.params.size) {
                fail
                    error "Invalid number of actual parameters."
                    source input
            }

            for (var i=0; i<input.exps.size; i++) {
                var inputExp = input.exps.get(i)
                var outputParam = outputScript.params.get(i)

                // get formal parameter type
                var formalParamType = env(G, outputParam, Type)
                // apply eventual substitutions to get the basic type
                formalParamType = typesubstitution(substitutions, formalParamType)
                // ensure it's basic
                failIfTypeVariable(formalParamType, outputParam)

                // all the actual parameters are well typed
                G |- substitutions |> inputExp : var Type actualParamType
                // apply eventual substitutions
                actualParamType = typesubstitution(substitutions, actualParamType)

                // the actual param type must be unifiable with the formal param type
                unify(inputExp, substitutions, actualParamType, formalParamType)

                // store the type in the AST
                outputParam.type = formalParamType as PermittedType
            }
        }
        else {
            // all the input expressions are well typed and the type is basic
            for (var i=0; i<input.exps.size; i++) {
                var exp = input.exps.get(i)
                G |- substitutions |> exp : var Type texp
                texp = typesubstitution(substitutions, texp)
                failIfTypeVariable(texp, exp)
            }
        }
    }
}

/**
 * An Output is well typed if:
 * - all the scripts parameters are well typed and the type is basic (not generic)
 * - all the scripts expressions are well typed
 */
rule Output
    G |- TypeSubstitutions substitutions |> Output _output : BitcoinTMFactory::eINSTANCE.createVoidType
from {
    var satoshis = _output.value.exp
    G |- new TypeSubstitutions() |> satoshis : var Type valType
    valType = typesubstitution(substitutions, valType)
    unify(satoshis, substitutions, valType, BitcoinTMFactory::eINSTANCE.createIntType)

    var script = _output.script
    for (p : script.params) {
        G |- substitutions |> p : var Type type
    }


    G |- substitutions |> script.exp : var Type scriptType
    scriptType = typesubstitution(substitutions, scriptType)
    
    if (script.params.size == 0) {
        {
            unify(script.exp, substitutions, scriptType, BitcoinTMFactory::eINSTANCE.createBooleanType)
        } or {
            unify(script.exp, substitutions, scriptType, BitcoinTMFactory::eINSTANCE.createStringType)
        } or {
            fail
            error "expected type boolean or string"
            source script.exp
        }     
    }
    else {
        unify(script.exp, substitutions, scriptType, BitcoinTMFactory::eINSTANCE.createBooleanType)
    }
    

    // all the parameters should have a basic type (generic types are not allowed)
    for (p : script.params) {
        var ptype = env(G, p, Type)
        ptype = typesubstitution(substitutions, ptype)
        failIfNotPermitted(ptype, p)
        p.type = ptype as PermittedType
    }
}

rule AbsoluteTime
    G |- TypeSubstitutions substitutions |> AbsoluteTime tlock : BitcoinTMFactory::eINSTANCE.createVoidType
from {
    G |- substitutions |> tlock.value : var Type type
    type = typesubstitution(substitutions, type)
    unify(tlock.value, substitutions, type, BitcoinTMFactory::eINSTANCE.createIntType)
}

rule RelativeTime
    G |- TypeSubstitutions substitutions |> RelativeTime tlock : BitcoinTMFactory::eINSTANCE.createVoidType
from {
    if (tlock.isBlock) {
        G |- substitutions |> tlock.value : var Type type
        type = typesubstitution(substitutions, type)
        unify(tlock.value, substitutions, type, BitcoinTMFactory::eINSTANCE.createIntType)
    }

    if (tlock.tx !== null) {
        G |- substitutions |> tlock.tx : var Type txtype
        txtype = typesubstitution(substitutions, txtype)
        unify(tlock.value, substitutions, txtype, BitcoinTMFactory::eINSTANCE.createTransactionType)
    }
}

/*
 * Expressions type system
 */
axiom NumeralLiteral
    G |- TypeSubstitutions substitutions |> NumberLiteral num : BitcoinTMFactory::eINSTANCE.createIntType

axiom BooleanLiteral
    G |- TypeSubstitutions substitutions |> BooleanLiteral bool : BitcoinTMFactory::eINSTANCE.createBooleanType

axiom StringLiteral
    G |- TypeSubstitutions substitutions |> StringLiteral str : BitcoinTMFactory::eINSTANCE.createStringType

axiom HashLiteral
    G |- TypeSubstitutions substitutions |> HashLiteral h : BitcoinTMFactory::eINSTANCE.createHashType

axiom TransactionHexLiteral
    G |- TypeSubstitutions substitutions |> TransactionHexLiteral tx : BitcoinTMFactory::eINSTANCE.createTransactionType

axiom TransactionIDLiteral
    G |- TypeSubstitutions substitutions |> TransactionIDLiteral tx : BitcoinTMFactory::eINSTANCE.createTransactionType

rule SignatureLiteral
    G |- TypeSubstitutions substitutions |> SignatureLiteral s : BitcoinTMFactory::eINSTANCE.createSignatureType
from {
    G |- substitutions |> s.key : var Type type
    type = typesubstitution(substitutions, type)
    unify(s.key, substitutions, type, BitcoinTMFactory::eINSTANCE.createPubkeyType)
}

axiom DateLiteral
    G |- TypeSubstitutions substitutions |> DateLiteral d : BitcoinTMFactory::eINSTANCE.createIntType

axiom KeyLiteral
    G |- TypeSubstitutions substitutions |> KeyLiteral key : BitcoinTMFactory::eINSTANCE.createKeyType

axiom AddressLiteral
    G |- TypeSubstitutions substitutions |> AddressLiteral addr : BitcoinTMFactory::eINSTANCE.createAddressType

axiom PubkeyLiteral
    G |- TypeSubstitutions substitutions |> PubKeyLiteral pk : BitcoinTMFactory::eINSTANCE.createPubkeyType



rule Times
    G |- TypeSubstitutions substitutions |> Times op : IntType t
from {
    t = BitcoinTMFactory::eINSTANCE.createIntType
    G |- substitutions |> op.left: var Type leftType
    G |- substitutions |> op.right: var Type rightType

    leftType = typesubstitution(substitutions, leftType)
    rightType = typesubstitution(substitutions, rightType)

    // the terms type must be unifiable with int type
    unify(op.left, substitutions, leftType, t)
    unify(op.right, substitutions, rightType, t)
}

rule Div
    G |- TypeSubstitutions substitutions |> Div op : IntType t
from {
    t = BitcoinTMFactory::eINSTANCE.createIntType
    G |- substitutions |> op.left: var Type leftType
    G |- substitutions |> op.right: var Type rightType

    leftType = typesubstitution(substitutions, leftType)
    rightType = typesubstitution(substitutions, rightType)

    // the terms type must be unifiable with int type
    unify(op.left, substitutions, leftType, t)
    unify(op.right, substitutions, rightType, t)
}

rule Minus
    G |- TypeSubstitutions substitutions |> Minus op : IntType t
from {
    t = BitcoinTMFactory::eINSTANCE.createIntType
    G |- substitutions |> op.left: var Type leftType
    G |- substitutions |> op.right: var Type rightType

    leftType = typesubstitution(substitutions, leftType)
    rightType = typesubstitution(substitutions, rightType)

    // the terms type must be unifiable with int type
    unify(op.left, substitutions, leftType, t)
    unify(op.right, substitutions, rightType, t)
}

rule Plus
    G |- TypeSubstitutions substitutions |> Plus op : Type type
from {
    G |- substitutions |> op.left : var Type leftType
    G |- substitutions |> op.right : var Type rightType

    leftType = typesubstitution(substitutions, leftType)
    rightType = typesubstitution(substitutions, rightType)

    {
        type = BitcoinTMFactory::eINSTANCE.createIntType
        // the terms type must be unifiable with int type
        unify(op.left, substitutions, leftType, type)
    }
    or
    {
        type = BitcoinTMFactory::eINSTANCE.createStringType
        // the terms type must be unifiable with string type
        unify(op.left, substitutions, leftType, type)
    }

    unify(op.right, substitutions, rightType, type)
}

rule Comparison
    G |- TypeSubstitutions substitutions |> Comparison op : BitcoinTMFactory::eINSTANCE.createBooleanType
from {
    var t = BitcoinTMFactory::eINSTANCE.createIntType
    G |- substitutions |> op.left : var Type leftType
    G |- substitutions |> op.right : var Type rightType

    leftType = typesubstitution(substitutions, leftType)
    rightType = typesubstitution(substitutions, rightType)

    unify(op.left, substitutions, leftType, t)
    unify(op.right, substitutions, rightType, t)
}

rule Equals
    G |- TypeSubstitutions substitutions |> Equals op : BitcoinTMFactory::eINSTANCE.createBooleanType
from {
    G |- substitutions |> op.left : var Type leftType
    G |- substitutions |> op.right : var Type rightType

    leftType = typesubstitution(substitutions, leftType)
    rightType = typesubstitution(substitutions, rightType)

    // equals between elements of the same type
    unify(op.right, substitutions, rightType, leftType)
}

rule BooleanNegation
    G |- TypeSubstitutions substitutions |> BooleanNegation negation : BooleanType boolType
from {
    boolType = BitcoinTMFactory::eINSTANCE.createBooleanType
    G |- substitutions |> negation.exp : var Type t

    t = typesubstitution(substitutions, t)

    unify(negation.exp, substitutions, t, boolType)
}

rule Or
    G |- TypeSubstitutions substitutions |> OrExpression orExp : BooleanType boolType
from {
    boolType = BitcoinTMFactory::eINSTANCE.createBooleanType
    G |- substitutions |> orExp.left : var Type leftType
    G |- substitutions |> orExp.right : var Type rightType

    leftType = typesubstitution(substitutions, leftType)
    rightType = typesubstitution(substitutions, rightType)

    unify(orExp.left, substitutions, leftType, boolType)
    unify(orExp.right, substitutions, rightType, boolType)
}

rule And
    G |- TypeSubstitutions substitutions |> AndExpression andExp : BooleanType boolType
from {
    boolType = BitcoinTMFactory::eINSTANCE.createBooleanType
    G |- substitutions |> andExp.left : var Type leftType
    G |- substitutions |> andExp.right : var Type rightType

    leftType = typesubstitution(substitutions, leftType)
    rightType = typesubstitution(substitutions, rightType)

    unify(andExp.left, substitutions, leftType, boolType)
    unify(andExp.right, substitutions, rightType, boolType)
}

rule ArithmeticSigned
    G |- TypeSubstitutions substitutions |> ArithmeticSigned signed : IntType intType
from {
    intType = BitcoinTMFactory::eINSTANCE.createIntType
    G |- substitutions |> signed.exp : var Type t

    t = typesubstitution(substitutions, t)

    unify(signed.exp, substitutions, t, intType)
}

rule IfThenElse
    G |- TypeSubstitutions substitutions |> IfThenElse stmt : Type type
from {
    var boolType = BitcoinTMFactory::eINSTANCE.createBooleanType
    G |- substitutions |> stmt.^if: var Type tIf
    G |- substitutions |> stmt.then: var Type tThen
    G |- substitutions |> stmt.^else: var Type tElse

    tIf = typesubstitution(substitutions, tIf)
    tThen = typesubstitution(substitutions, tThen)
    tElse = typesubstitution(substitutions, tElse)

    unify(stmt.^if, substitutions, tIf, boolType)
    unify(stmt.^else, substitutions, tElse, tThen)

    type=tThen
}

rule VerSig
    G |- TypeSubstitutions substitutions |> Versig versig : BitcoinTMFactory::eINSTANCE.createBooleanType
from {
    var sigType = BitcoinTMFactory::eINSTANCE.createSignatureType

    for (s : versig.signatures) {
        G |- substitutions |> s : var Type ts
        ts = typesubstitution(substitutions, ts)
        unify(s, substitutions, ts, sigType)
    }

    val script = EcoreUtil2.getContainerOfType(versig, Script)
    var addrType =  if (script.isP2PKH) BitcoinTMFactory::eINSTANCE.createAddressType
					else BitcoinTMFactory::eINSTANCE.createPubkeyType

    for (k : versig.pubkeys) {
        G |- substitutions |> k : var Type t
        t = typesubstitution(substitutions, t)
        unify(k, substitutions, t, addrType)
    }
}

rule Signature
    G |- TypeSubstitutions substitutions |> Signature s : BitcoinTMFactory::eINSTANCE.createSignatureType
from {
    var keyType = BitcoinTMFactory::eINSTANCE.createKeyType

    G |- substitutions |> s.privkey : var Type t
    t = typesubstitution(substitutions, t)
    unify(s.privkey, substitutions, t, keyType)

    if (s.tx !== null) {
        var txType = BitcoinTMFactory::eINSTANCE.createTransactionType

        G |- substitutions |> s.tx : var Type t1
        t1 = typesubstitution(substitutions, t1)
        unify(s.tx, substitutions, t1, txType)
    }
}

rule Hash160
    G |- TypeSubstitutions substitutions |> Hash160 hash: BitcoinTMFactory::eINSTANCE.createHashType
from {
    G |- substitutions |> hash.value: var Type t
    t = typesubstitution(substitutions, t)

    if (!(t instanceof HashableType)) {
        fail
        error "Cannot hash type "+stringRep(t)
        source hash.value
    }
}

rule Hash256
    G |- TypeSubstitutions substitutions |> Hash256 hash: BitcoinTMFactory::eINSTANCE.createHashType
from {
    G |- substitutions |> hash.value: var Type t
    t = typesubstitution(substitutions, t)

    if (!(t instanceof HashableType)) {
        fail
        error "Cannot hash type "+stringRep(t)
        source hash.value
    }
}

rule Ripemd160
    G |- TypeSubstitutions substitutions |> Ripemd160 hash: BitcoinTMFactory::eINSTANCE.createHashType
from {
    G |- substitutions |> hash.value: var Type t
    t = typesubstitution(substitutions, t)

    if (!(t instanceof HashableType)) {
        fail
        error "Cannot hash type "+stringRep(t)
        source hash.value
    }
}

rule Sha256
    G |- TypeSubstitutions substitutions |> Sha256 hash: BitcoinTMFactory::eINSTANCE.createHashType
from {
    G |- substitutions |> hash.value: var Type t
    t = typesubstitution(substitutions, t)

    if (!(t instanceof HashableType)) {
        fail
        error "Cannot hash type "+stringRep(t)
        source hash.value
    }
}

rule Sha1
    G |- TypeSubstitutions substitutions |> Sha1 hash: BitcoinTMFactory::eINSTANCE.createHashType
from {
    G |- substitutions |> hash.value: var Type t
    t = typesubstitution(substitutions, t)

    if (!(t instanceof HashableType)) {
        fail
        error "Cannot hash type "+stringRep(t)
        source hash.value
    }
}
rule Min
    G |- TypeSubstitutions substitutions |> Min min: IntType t
from {
    t = BitcoinTMFactory::eINSTANCE.createIntType
    G |- substitutions |> min.left: var Type tleft
    G |- substitutions |> min.right: var Type tright

    tleft = typesubstitution(substitutions, tleft)
    tright = typesubstitution(substitutions, tright)

    unify(min.left, substitutions, tleft, t)
    unify(min.right, substitutions, tright, t)
}

rule Max
    G |- TypeSubstitutions substitutions |> Max max: IntType t
from {
    t = BitcoinTMFactory::eINSTANCE.createIntType
    G |- substitutions |> max.left: var Type tleft
    G |- substitutions |> max.right: var Type tright

    tleft = typesubstitution(substitutions, tleft)
    tright = typesubstitution(substitutions, tright)

    unify(max.left, substitutions, tleft, t)
    unify(max.right, substitutions, tright, t)
}

rule Between
    G |- TypeSubstitutions substitutions |> Between between: IntType t
from {
    t = BitcoinTMFactory::eINSTANCE.createIntType
    G |- substitutions |> between.value: var Type tValue
    G |- substitutions |> between.left: var Type tLeft
    G |- substitutions |> between.right: var Type tRight

    tValue = typesubstitution(substitutions, tValue)
    tLeft = typesubstitution(substitutions, tLeft)
    tRight = typesubstitution(substitutions, tRight)

    unify(between.value, substitutions, tValue, t)
    unify(between.left, substitutions, tLeft, t)
    unify(between.right, substitutions, tRight, t)
}

rule Size
    G |- TypeSubstitutions substitutions |> Size size: BitcoinTMFactory::eINSTANCE.createIntType
from {
    G |- substitutions |> size.value: var Type t

    t = typesubstitution(substitutions, t)
}

rule AfterTimeLock
    G |- TypeSubstitutions substitutions |> AfterTimeLock after: Type texpr
from {
    G |- substitutions |> after.timelock: var Type t

    G |- substitutions |> after.continuation: texpr
    texpr = typesubstitution(substitutions, texpr)
}

axiom InputValue
    G |- TypeSubstitutions substitutions |> InputValue inval: BitcoinTMFactory::eINSTANCE.createIntType


rule Compile
    G |- TypeSubstitutions substitutions |> Compile compile: BitcoinTMFactory::eINSTANCE.createVoidType
from {
    for (e : compile.txs) {
        G |- substitutions |> e: var Type texpr
        texpr = typesubstitution(substitutions, texpr)
        failIfNotPermitted(texpr, e)
    }
}

/*
 *
 * rule invocations
 *
 */

checkrule CheckConstants for
    Constant stmt
from {
    lambdaUtils.resetCounter
    empty |- new TypeSubstitutions |> stmt: var Type t
}

checkrule CheckTransactions for
    Transaction stmt
from {
    lambdaUtils.resetCounter
    empty |- new TypeSubstitutions |> stmt: var Type t
}

checkrule CheckCompile for
    Compile stmt
from {
    lambdaUtils.resetCounter
    empty |- new TypeSubstitutions |> stmt: var Type t
}
